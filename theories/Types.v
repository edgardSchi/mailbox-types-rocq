(** * Syntax of types *)

From Stdlib Require Import Lia.
From Stdlib Require Import Classes.RelationClasses.

From MailboxTypes Require Export Message.
From MailboxTypes Require Export MailboxPatterns.

From Stdlib Require Import List.
Import ListNotations.

Generalizable All Variables.

Section type_def.

Context `{M : IMessage Message}.

(** Mailbox type definition *)
Inductive MType : Type :=
    MTOutput : MPattern -> MType
  | MTInput  : MPattern -> MType.

(** Base type definition. For now only unit type and booleans *)
Inductive BType : Type :=
    BTUnit : BType
| BTBool : BType.

(** Type definition. A type is either a base type or a mailbox type *)
Inductive TType : Type :=
    TTBase : BType -> TType
  | TTMailbox : MType -> TType.

(** Usage annotation for quasi-linear typing *)
Inductive UsageAnnotation : Type :=
    SecondClass
  | Returnable.

(** Quasi-linear types are types equipped with a usage annotation.
    Base types have no restrictions. Mailbox types are either second class
   or returnable.
*)
Inductive TUsage : Type :=
    TUBase : BType -> TUsage
  | TUUsage : UsageAnnotation -> MType -> TUsage.

Definition returnType (t : TType) : TUsage :=
  match t with
  | TTBase b => TUBase b
  | TTMailbox m => TUUsage Returnable m
  end.

Definition returnUsage (t : TUsage) : TUsage :=
  match t with
  | TUBase b => TUBase b
  | TUUsage _ m => TUUsage Returnable m
  end.

Definition secondType (t : TType) : TUsage :=
  match t with
  | TTBase b => TUBase b
  | TTMailbox m => TUUsage SecondClass m
  end.

Definition secondUsage (t : TUsage) : TUsage :=
  match t with
  | TUBase b => TUBase b
  | TUUsage _ m => TUUsage SecondClass m
  end.

Definition BaseType (T : TType) : Prop :=
  match T with
  | TTBase _ => True
  | _ => False
  end.

(** A list of only base types *)
Fixpoint BaseTypes (e : list TType) : Prop :=
  match e with
  | nil => True
  | (TTBase _ :: e') => BaseTypes e'
  | _ => False
  end.

Inductive UsageSubtype : UsageAnnotation -> UsageAnnotation -> Prop :=
    UsageSubtypeRefl : forall n, UsageSubtype n n
  | UsageSubtypeLe   : UsageSubtype Returnable SecondClass.

Inductive Subtype : TUsage -> TUsage -> Prop :=
    SubtypeBase : forall c, Subtype (TUBase c) (TUBase c)
  | SubtypeInput : forall e f n1 n2,
      MPInclusion e f -> UsageSubtype n1 n2 -> Subtype (TUUsage n1 (MTInput e)) (TUUsage n2 (MTInput f))
  | SubtypeOutput : forall e f n1 n2,
      MPInclusion f e -> UsageSubtype n1 n2 -> Subtype (TUUsage n1 (MTOutput e)) (TUUsage n2 (MTOutput f)).

Definition TypeEqual (a b : TUsage) : Prop :=
  Subtype a b /\ Subtype b a.

End type_def.

(** ** Notation for types *)
Declare Scope types_scope.
Open Scope types_scope.

Notation "! E" := (MTOutput E) (at level 73) : types_scope.
Notation "? E" := (MTInput E) (at level 73) : types_scope.
Notation "‚ó¶" := (SecondClass) : types_scope.
Notation "‚Ä¢" := (Returnable) : types_scope.
Notation "T ^^ n" := (TUUsage n T) (at level 75) : types_scope.
Notation "‚åä T ‚åã" := (returnType T) : types_scope.
Notation "‚åä T ‚åã‚Åø" := (returnUsage T) : types_scope.
Notation "‚åà T ‚åâ" := (secondType T) : types_scope.
Notation "‚åà T ‚åâ‚Åø" := (secondUsage T) : types_scope.
Notation "n1 ‚â§‚Åø n2" := (UsageSubtype n1 n2) (at level 80) : types_scope.
Notation "A ‚â§ B" := (Subtype A B) (at level 80) : types_scope.
Notation "A ‚âÉ B" := (TypeEqual A B) (at level 80) : types_scope.

Section mailbox_types_classes.

Context `{M : IMessage Message}.

(* TODO: Check if this is correct *)
Definition Relevant (m : MType) : Prop :=
  forall n, ~ ((TUUsage n m) ‚â§ (TUUsage n (! ùüô))).

Definition Irrelevant (m : MType) : Prop :=
  forall n, (m ^^ n) ‚â§ (! ùüô ^^ n).

Definition Reliable (m : MType) : Prop :=
  forall n, ~ ((TUUsage n m) ‚â§ (TUUsage n (? ùüò))).

Definition Unreliable (m : MType) : Prop :=
  ~ Reliable m.

Definition Usable (m : MType) : Prop :=
  forall n, ~ ((TUUsage n m) ‚â§ (TUUsage n (! ùüò))).

Definition Unusable (m : MType) : Prop :=
  ~ Usable m.

(* TODO: Check if this is correct *)

(* Old Version *)
(*Inductive Unrestricted : TUsage -> Prop :=*)
(*    unBase : forall c : BType, Unrestricted (TUBase c)*)
(*  | unOne : Unrestricted (TUUsage SecondClass (! ùüô)).*)

(** In the original paper, the predicate Unrestricted is defined over a syntactic
    check, i.e. Un(T) <=> T = ! ùüô ^^ ‚ó¶.

    However, this definition breaks transitivity of environment subtyping. As an
    example, consider environment [[! ùüô ^^ ‚Ä¢]]. The type [! ùüô ^^ ‚Ä¢] is not unrestricted,
    so, [[! ùüô ^^ ‚Ä¢] ‚â§‚Çë []] does not hold. But [! ùüô ^^ ‚Ä¢ ‚â§ ! ùüô ^^ ‚ó¶] holds, and 
    [[! ùüô ^^ ‚ó¶] ‚â§‚Çë []] holds since the type is unrestricted.

    So in the typing rules we would have to use the [TSub]-rule twice in order to
    weaken the environment from [[! ùüô ^^ ‚Ä¢]] to [[]].
    To avoid this, we define a type to be unrestricted, if it is a subtype of [! ùüô ^^ ‚ó¶].
*)

Inductive Unrestricted : TUsage -> Prop :=
    unBase : forall c : BType, Unrestricted (TUBase c)
  | unOne : Unrestricted (! ùüô ^^ ‚ó¶).

Definition Linear (m : TUsage) : Prop :=
  ~ Unrestricted m.

(* TODO: Maybe remove these definitions if they are not needed *)

(** After changing the definition of un(-), I think the notion of 
    Cruft is not needed anymore.
*)

(** A cruft type is either a base type or irrelevant.
    A cruft type represents a type that can be added
   through the subtyping rule which may not be used
   by a term.
*)
Definition TTCruft (t : TType) : Prop :=
  match t with
  | TTBase _ => True
  | TTMailbox m => Irrelevant m
  end.

(** A cruft usage-type is either a base type or the usage is
    secondClass and the type is cruft
*)
Definition TUCruft (t : TUsage) : Prop :=
  match t with
  | TUBase _ => True
  | TUUsage SecondClass ty => TTCruft (TTMailbox ty)
  | _ => False
  end.

End mailbox_types_classes.

Section mailbox_combinations.

  Context `{M : IMessage Message}.
(** 
   Definition 3.5 of type combiniations. Instead of defining it as a partial function,
   we define it as a relation between three types.
*)
  Inductive TypeCombination : TType -> TType -> TType -> Prop :=
      TCombBase : forall c, TypeCombination (TTBase c) (TTBase c) (TTBase c)
    | TCombOut : forall e f, TypeCombination (TTMailbox (! e)) (TTMailbox (! f)) (TTMailbox (! (e ‚äô f)))
    (*| TCombOut_2 : forall e f, TypeCombination (TTMailbox (! e)) (TTMailbox (! f)) (TTMailbox (! (f ‚äô e)))*)
    | TCombInOut : forall e f, TypeCombination (TTMailbox (! e)) (TTMailbox (? (e ‚äô f))) (TTMailbox (? f))
    (*| TCombInOut_2 : forall e f, TypeCombination (TTMailbox (! e)) (TTMailbox (? (f ‚äô e))) (TTMailbox (? f))*)
    | TCombOutIn : forall e f, TypeCombination (TTMailbox (? (e ‚äô f))) (TTMailbox (! e)) (TTMailbox (? f)).
    (*| TCombOutIn_2 : forall e f, TypeCombination (TTMailbox (? (f ‚äô e))) (TTMailbox (! e)) (TTMailbox (? f)).*)

(** 
   Definition 3.6 of usage combiniations. Again, instead of defining it as a partial function,
   we define it as a relation between three usage annotations.
*)
  Inductive UsageCombination : UsageAnnotation -> UsageAnnotation -> UsageAnnotation -> Prop :=
      UCombSecond : UsageCombination SecondClass SecondClass SecondClass
    | UCombSecRet : UsageCombination SecondClass Returnable Returnable.

(** 
   Definition 3.7 of usage-annotated type combiniations.
   Again, instead of defining it as a partial function,
   we define it as a relation between three types.
*)
  Inductive TypeUsageCombination : TUsage -> TUsage -> TUsage -> Prop :=
      TUsageCombBase : forall c, TypeUsageCombination (TUBase c) (TUBase c) (TUBase c)
    | TUsageCombUsage : forall j k n1 n2 n t,
        UsageCombination n1 n2 n ->
        TypeCombination (TTMailbox j) (TTMailbox k) (TTMailbox t) ->
        TypeUsageCombination (TUUsage n1 j) (TUUsage n2 k) (TUUsage n t).
End mailbox_combinations.

Notation "T ‚äû U ~= V" := (TypeCombination T U V) (at level 80) : types_scope.
Notation "n1 ‚ñ∑‚Åø n2 ~= n" := (UsageCombination n1 n2 n) (at level 80) : types_scope.
Notation "J ‚ñ∑ K ~= L" := (TypeUsageCombination J K L) (at level 80) : types_scope.


Section mailbox_types_properties.
  Context `{M : IMessage Message}.

  Lemma secondUsage_idem : forall T, ‚åà T ‚åâ‚Åø = ‚åà ‚åà T ‚åâ‚Åø ‚åâ‚Åø.
  Proof.
    destruct T; try destruct b; easy.
  Qed.

  (** Subtyping preserves reliability.
      Proof based on Ugo de‚ÄôLiguoro and Luca Padovani Prop. 26
      In Fowler et al. this is Lemma D.6
  *)
  Lemma SubtypeReliable : forall a b n1 n2, (a ^^ n1) ‚â§ (b ^^ n2) -> Reliable a -> Reliable b.
  Proof.
    intros a b n1 n2 Sub_a Rel.
    unfold Reliable in *.
    destruct a; inversion Sub_a as [| ? ? ? ? Inc_mf |]; subst;
    intros n Sub_b; inversion Sub_b as [| ? ? ? ? Inc_f0 |]; subst.
    generalize (MPInclusion_trans m f ùüò Inc_mf Inc_f0).
    intros Inc0.
    destruct Rel with (n := n).
    constructor; easy.
  Qed.

  (** Subtyping preserves usability.
      Proof based on Ugo de‚ÄôLiguoro and Luca Padovani Prop. 26
      In Fowler et al. this is Lemma D.6
  *)
  Lemma SubtypeUsable : forall a b n1 n2, (a ^^ n1) ‚â§ (b ^^ n2) -> Usable a -> Usable b.
  Proof.
    intros a b n1 n2 Sub_a Use.
    unfold Usable in *.
    destruct a; inversion Sub_a as [| | ? ? ? ? Inc_fm ]; subst;
    intros n Sub_b; inversion Sub_b as [| | ? ? ? ? Inc_0f]; subst.
    generalize (MPInclusion_trans ùüò f m Inc_0f Inc_fm).
    intros Inc0.
    destruct Use with (n := n).
    constructor; easy.
Qed.

Lemma UsageSubtype_trans : forall n1 n2 n3, n1 ‚â§‚Åø n2 -> n2 ‚â§‚Åø n3 -> n1 ‚â§‚Åø n3.
Proof.
  intros * Sub1 Sub2; destruct n1, n2, n3; try (constructor); assumption.
Qed.

Global Instance UsageSubtype_refl : Reflexive UsageSubtype.
Proof.
  unfold Reflexive; constructor.
Qed.

Lemma Subtype_refl : forall t, t ‚â§ t.
Proof.
  destruct t.
  - constructor.
  - destruct m; constructor; try (apply MPInclusion_refl || constructor).
Qed.

Lemma Subtype_trans : forall t1 t2 t3, t1 ‚â§ t2 -> t2 ‚â§ t3 -> t1 ‚â§ t3.
Proof.
  intros * Sub1 Sub2.
  induction Sub1; inversion Sub2; subst; constructor;
  try (
    now apply MPInclusion_trans with (f := f) ||
    now apply UsageSubtype_trans with (n2 := n2)
  ).
Qed.

Lemma Subtype_inversion_TUBase_right : forall T B, T ‚â§ (TUBase B) -> T = TUBase B.
Proof.
  intros * Sub.
  now inversion Sub.
Qed.

Lemma Subtype_inversion_TUBase_left : forall T B, (TUBase B) ‚â§ T -> T = TUBase B.
Proof.
  intros * Sub.
  now inversion Sub.
Qed.

(*Lemma TypeCombination_comm : forall T1 T2 T3, T1 ‚äû T2 ~= T3 -> T2 ‚äû T1 ~= T3.*)
(*Proof.*)
(*  destruct T1, T2, T3; intros; try inversion H; subst;*)
(*  try assumption; try constructor.*)
(*Qed.*)

(* TODO: Remove this. Does not hold if Unrestricted is a syntactic check *)
(*Lemma Subtype_preserves_Unrestricted : forall T1 T2, Unrestricted T2 -> T1 ‚â§ T2 -> Unrestricted T1.*)
(*Proof.*)
(*  intros * Unr2 Sub.*)
(*  inversion Unr2; subst.*)
(*  - inversion Sub; subst; constructor.*)
(*  - constructor; now apply Subtype_trans with (t2 := T2).*)
(*Qed.*)

Lemma Unrestricted_implies_Cruft : forall T, Unrestricted T -> TUCruft T.
Proof.
  intros * Unr; inversion Unr; constructor; reflexivity.
Qed.

Lemma Subtype_Irrelevant : forall T1 T2 n1 n2,
  Irrelevant T2 -> T1 ^^ n1  ‚â§ T2 ^^ n2 -> Irrelevant T1.
Proof.
  intros * Irr Sub.
  unfold Irrelevant in *.
  intros n'.
  inversion Sub; subst.
  - inversion H4; subst.
    + generalize (Irr SecondClass); intros I; inversion I.
    + generalize (Irr SecondClass); intros I; inversion I.
  - destruct n'.
    assert (SubSecond : ! e ^^ ‚ó¶ ‚â§ ! f ^^ ‚ó¶) by now constructor.
    eauto using Subtype_trans.
    assert (SubReturn : ! e ^^ ‚Ä¢ ‚â§ ! f ^^ ‚Ä¢) by now constructor.
    eauto using Subtype_trans.
Qed.

(*Lemma Subtype_Cruft : forall T1 T2, TUCruft T2 -> T1 ‚â§ T2 -> TUCruft T1.*)
(*Proof.*)
(*  intros * Cruft Sub.*)
(*  destruct T2.*)
(*  - now inversion Sub.*)
(*  - simpl in Cruft.*)
(*    destruct u.*)
(*    + destruct T1.*)
(*      * constructor.*)
(*      * generalize (Subtype_Irrelevant _ _ _ _ Cruft Sub).*)
(*        intros Irr.*)
(*        destruct u.*)
(*        -- easy.*)
(*        -- simpl.*)
(*           unfold Irrelevant in *.*)
(*           inversion Sub; subst.*)
(*           ++ generalize (Cruft SecondClass); intros I; inversion I.*)
(*           ++ unfold Irrelevant in *.*)
(**)
(*      apply Subtype_Irrelevant in Cruft.*)
(*      inversion Sub; subst.*)
(*      * generalize (Cruft SecondClass).*)
(*        intros I; inversion I.*)
(*      * inversion H3; subst.*)
(*        -- generalize (Cruft SecondClass).*)
(*           intros.*)
(*           inversion H; subst.*)
(*           simpl. constructor.*)
(*           ++ eapply MPInclusion_trans; eassumption.*)
(*           ++ reflexivity.*)
(*        -- generalize (Cruft SecondClass).*)
(*           intros.*)
(*           simpl.*)

(** Leibniz equality of mailbox types is decidable *)
Lemma MType_eq_dec : forall (T1 T2 : MType), {T1 = T2} + {T1 <> T2}.
Proof.
  destruct T1 as [m1 | m1], T2 as [m2 | m2];
  try (right; intros N; discriminate);
  case (MPattern_eq_dec m1 m2); intros Eq; subst;
  try (now left);
  right; intros N; now inversion N.
Qed.

(** Leibniz equality of types is decidable *)
Lemma TType_eq_dec : forall (T1 T2 : TType), {T1 = T2} + {T1 <> T2}.
Proof.
  destruct T1, T2;
  try (right; intros N; discriminate).
  - destruct b, b0;
    try (now left);
    try (right; intros N; discriminate).
  - case (MType_eq_dec m m0); intros; subst.
    now left.
    right; intros N; now inversion N.
Qed.

(** Leibniz equality of usage types is decidable *)
Lemma TUsage_eq_dec : forall (T1 T2 : TUsage), {T1 = T2} + {T1 <> T2}.
Proof.
  destruct T1, T2;
  try (right; intros N; discriminate).
  - destruct b, b0;
    try (now left);
    try (right; intros N; discriminate).
  - destruct u, u0;
    try (right; intros N; discriminate);
    case (MType_eq_dec m m0); intros; subst;
    try (now left);
    try (right; intros N; now inversion N).
Qed.

(** Deciding whether a type is unrestricted is deciable *)
Lemma Unrestricted_dec : forall T, {Unrestricted T} + {~ Unrestricted T}.
Proof.
  intros T; destruct T.
  - left; constructor.
  - destruct m; destruct m; destruct u;
    try (left; constructor);
    right; intros Unr; inversion Unr.
Qed.

End mailbox_types_properties.
